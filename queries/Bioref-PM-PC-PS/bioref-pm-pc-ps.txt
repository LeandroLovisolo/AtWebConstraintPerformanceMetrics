# Topic Bioref-PM-PC-PS
#
# Included experiments are composed of a least one pre-milling step, followed
# by a physico-chemical treatment and a washing and filtration step (washing
# and separation), and finally the enzymatic hydrolysis step. (en)

prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix bioraf: <http://opendata.inra.fr/resources/BIORAF#>
prefix anno: <http://opendata.inra.fr/resources/atWeb/annotation/>

SELECT ?docid ?doctitle ?tableid ?tabletitle
       ?rownum1 ?expnum1 ?stepnum1 ?milling
       ?rownum2 ?expnum2 ?stepnum2 ?physico_chemical_treatment
       ?rownum3 ?expnum3 ?stepnum3
WHERE {

# Placeholder #docid# is replaced from Java code
BIND(#docid# as ?docid)

# bioraf:milling and all its subclasses
VALUES ?milling {
  bioraf:milling
  bioraf:chopping
  bioraf:hammer_milling
  bioraf:two_45_roll_milling
  bioraf:colloid_milling
  bioraf:vibro_energy_milling
  bioraf:cutting_milling
  bioraf:disk_milling
  bioraf:wet_disk_milling
  bioraf:ball_milling
  bioraf:dry_ball_milling
  bioraf:centrifugal_milling
  bioraf:manual_milling
  bioraf:jet_milling
}

# bioraf:chemical_and_physico_45_chemical_pretreatment and all its subclasses
VALUES ?physico_chemical_treatment {
  bioraf:chemical_and_physico_45_chemical_pretreatment
  bioraf:wet_explosion_tretment
  bioraf:acid_treatment
  bioraf:phosphoric_acid_treatment
  bioraf:sulfuric_acid_treatment
  bioraf:hydrochloric_acid_treatment
  bioraf:screw_extrusion_and_steam_explosion_treatment
  bioraf:washing_and_separation
  bioraf:washing_and_press
  bioraf:washing_and_filtration
  bioraf:washing_and_centrifugation
  bioraf:washing_and_sieving
  bioraf:steam_treatment
  bioraf:impregnation
  bioraf:salt_treatment
  bioraf:sodium_bisulfite_treatment
  bioraf:oxidizing_agents_treatment
  bioraf:hydrogen_peroxide_treatment
  bioraf:wet_oxidation_treatment
  bioraf:ozone_treatment
  bioraf:hot_water_treatment
  bioraf:explosion_treatment
  bioraf:steam_explosion_treatment
  bioraf:so2_explosion_treatment
  bioraf:ammonia_fiber_explosion_treatment
  bioraf:co2_explosion_treatment
  bioraf:solvent_extraction_treatment
  bioraf:organosolv_treatment
  bioraf:swelling_agents_treatment
  bioraf:benzene_45_water_extraction_treatment
  bioraf:butanol_45_water_extraction_treatment
  bioraf:ethanol_45_water_extraction_treatment
  bioraf:solvent_extraction_non_organosolv_treatment
  bioraf:ethylene_45_glycol_extraction_treatment
  bioraf:alkali_treatment
  bioraf:sodium_hydroxide_treatment
  bioraf:lime_treatment
  bioraf:ammonia_treatment
  bioraf:ammonium_sulfite_treatment
  bioraf:gaz_treatment
  bioraf:nitrogen_dioxide_treatment
  bioraf:chlorine_dioxide_treatment
  bioraf:sulfur_dioxide_treatment
  bioraf:carbon_dioxide_treatment
}

?doc anno:hasForID ?docid ;
     dc:title ?doctitle ;
     anno:hasTable [anno:hasForID ?tableid ;
                    dc:title ?tabletitle ;
                    anno:hasForRow ?row1, ?row2, ?row3] .

?row1 anno:hasForRowNumber ?rownum1 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum1] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum1] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a ?milling .

?row2 anno:hasForRowNumber ?rownum2 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum2] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum2] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a ?physico_chemical_treatment .

?row3 anno:hasForRowNumber ?rownum3 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum3] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum3] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a bioraf:enzymatic_hydrolysis_treatment .

FILTER (?expnum1 = ?expnum2 &&
        ?expnum2 = ?expnum3 &&
        xsd:float(?stepnum1) < xsd:float(?stepnum2) &&
        xsd:float(?stepnum2) < xsd:float(?stepnum3))
}

# Topic Bioref-PM
#
# It contains experiments with only one milling followed by the enzymatic
# hydrolysis (Pre-Milling). It does not include a physico-chemical step but it
# can include a washing and separation step. All control experiments should be
# indexed in this topic. (en)

prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix bioraf: <http://opendata.inra.fr/resources/BIORAF#>
prefix anno: <http://opendata.inra.fr/resources/atWeb/annotation/>

SELECT ?docid ?doctitle ?tableid ?tabletitle
       ?rownum1 ?expnum1 ?stepnum1 ?milling
       ?rownum2 ?expnum2 ?stepnum2
WHERE {

# Placeholder #docid# is replaced from Java code
BIND(#docid# as ?docid)

VALUES ?milling {
  bioraf:chopping
  bioraf:hammer_milling
  bioraf:two_45_roll_milling
  bioraf:colloid_milling
  bioraf:vibro_energy_milling
  bioraf:cutting_milling
  bioraf:disk_milling
  bioraf:wet_disk_milling
  bioraf:ball_milling
  bioraf:dry_ball_milling
  bioraf:centrifugal_milling
  bioraf:manual_milling
  bioraf:jet_milling
}

?doc anno:hasForID ?docid ;
     dc:title ?doctitle ;
     anno:hasTable [anno:hasForID ?tableid ;
                    dc:title ?tabletitle ;
                    anno:hasForRow ?row1, ?row2] .

?row1 anno:hasForRowNumber ?rownum1 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum1] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum1] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a ?milling .

?row2 anno:hasForRowNumber ?rownum2 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum2] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum2] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a bioraf:enzymatic_hydrolysis_treatment .

FILTER (?expnum1 = ?expnum2 &&
        xsd:float(?stepnum1) < xsd:float(?stepnum2))
}

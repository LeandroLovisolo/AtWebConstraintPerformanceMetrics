# Topic Bioref-PM-UFM
#
# Included experiments are composed of a pre-milling step followed by a second
# milling step (ball milling, wet disk milling, etc.), and finally the
# enzymatic hydrolysis step. It does not include a physic-chemical step but it
# can include a washing and separation step. The second milling step must give
# an “Output solid constituent size” smaller than 0,5-1 mm.  (en)

prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix bioraf: <http://opendata.inra.fr/resources/BIORAF#>
prefix anno: <http://opendata.inra.fr/resources/atWeb/annotation/>

SELECT ?docid ?doctitle ?tableid ?tabletitle
       ?rownum1 ?stepnum1 ?expnum ?milling
       ?rownum2 ?stepnum2 ?physico_chemical_treatment
       ?rownum3 ?stepnum3
WHERE {

# Placeholder #docid# is replaced from Java code
BIND(#docid# as ?docid)

# bioraf:milling and all its subclasses
VALUES ?milling {
  bioraf:milling
  bioraf:chopping
  bioraf:hammer_milling
  bioraf:two_45_roll_milling
  bioraf:colloid_milling
  bioraf:vibro_energy_milling
  bioraf:cutting_milling
  bioraf:disk_milling
  bioraf:wet_disk_milling
  bioraf:ball_milling
  bioraf:dry_ball_milling
  bioraf:centrifugal_milling
  bioraf:manual_milling
  bioraf:jet_milling
}

# bioraf:milling subclasses that produce an output solid constituent size < 1mm
# (all millings except for bioraf:cutting_milling and bioraf:hammer_milling)
VALUES ?ultra_fine_milling {
  bioraf:chopping
  bioraf:two_45_roll_milling
  bioraf:colloid_milling
  bioraf:vibro_energy_milling
  bioraf:disk_milling
  bioraf:wet_disk_milling
  bioraf:ball_milling
  bioraf:dry_ball_milling
  bioraf:centrifugal_milling
  bioraf:manual_milling
  bioraf:jet_milling
}

# bioraf:chemical_and_physico_45_chemical_pretreatment and all its subclasses
# minus bioraf:washing_and_separation and all its subclasses
VALUES ?physico_chemical_treatment_minus_washing_and_separation {
  bioraf:chemical_and_physico_45_chemical_pretreatment
  bioraf:wet_explosion_tretment
  bioraf:acid_treatment
  bioraf:phosphoric_acid_treatment
  bioraf:sulfuric_acid_treatment
  bioraf:hydrochloric_acid_treatment
  bioraf:screw_extrusion_and_steam_explosion_treatment
  bioraf:steam_treatment
  bioraf:impregnation
  bioraf:salt_treatment
  bioraf:sodium_bisulfite_treatment
  bioraf:oxidizing_agents_treatment
  bioraf:hydrogen_peroxide_treatment
  bioraf:wet_oxidation_treatment
  bioraf:ozone_treatment
  bioraf:hot_water_treatment
  bioraf:explosion_treatment
  bioraf:steam_explosion_treatment
  bioraf:so2_explosion_treatment
  bioraf:ammonia_fiber_explosion_treatment
  bioraf:co2_explosion_treatment
  bioraf:solvent_extraction_treatment
  bioraf:organosolv_treatment
  bioraf:swelling_agents_treatment
  bioraf:benzene_45_water_extraction_treatment
  bioraf:butanol_45_water_extraction_treatment
  bioraf:ethanol_45_water_extraction_treatment
  bioraf:solvent_extraction_non_organosolv_treatment
  bioraf:ethylene_45_glycol_extraction_treatment
  bioraf:alkali_treatment
  bioraf:sodium_hydroxide_treatment
  bioraf:lime_treatment
  bioraf:ammonia_treatment
  bioraf:ammonium_sulfite_treatment
  bioraf:gaz_treatment
  bioraf:nitrogen_dioxide_treatment
  bioraf:chlorine_dioxide_treatment
  bioraf:sulfur_dioxide_treatment
  bioraf:carbon_dioxide_treatment
}

?doc anno:hasForID ?docid ;
     dc:title ?doctitle ;
     anno:hasTable [anno:hasForID ?tableid ;
                    dc:title ?tabletitle ;
                    anno:hasForRow ?row1, ?row2, ?row3] .

?row1 anno:hasForRowNumber ?rownum1 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum1] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a ?milling .

?row2 anno:hasForRowNumber ?rownum2 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum2] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a ?ultra_fine_milling .

?row3 anno:hasForRowNumber ?rownum3 ;
      anno:hasForCell [a bioraf:experience_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?expnum] ;
      anno:hasForCell [a bioraf:process_step_number ;
                       anno:hasForFS /
                       anno:hasForFuzzyElement /
                       anno:hasForMinKernel ?stepnum3] ;
      anno:hasForCell /
      anno:hasForFS /
      anno:hasForElement /
      a bioraf:enzymatic_hydrolysis_treatment .

FILTER NOT EXISTS {
  ?doc anno:hasTable /
       anno:hasForRow [anno:hasForCell [a bioraf:experience_number ;
                                        anno:hasForFS /
                                        anno:hasForFuzzyElement /
                                        anno:hasForMinKernel ?expnum] ;
                       anno:hasForCell /
                       anno:hasForFS /
                       anno:hasForElement /
                       a ?physico_chemical_treatment_minus_washing_and_separation] ;
}

FILTER (xsd:float(?stepnum1) < xsd:float(?stepnum2) &&
        xsd:float(?stepnum2) < xsd:float(?stepnum3))
}
